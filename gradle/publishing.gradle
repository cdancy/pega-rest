buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
    }
}

apply plugin: 'maven-publish'
apply plugin: com.jfrog.bintray.gradle.BintrayPlugin

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            artifact shadowJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', "${project.name}")
                root.appendNode('description', "${project.description}")
                root.appendNode('url', "https://github.com/${project.organization}/${project.name}")
                root.appendNode('inceptionYear', '2016')

                def scm = root.appendNode('scm')
                scm.appendNode('url', "https://github.com/${project.organization}/${project.name}")
                scm.appendNode('connection', "scm:https://${project.organization}@github.com/${project.organization}/${project.name}.git")
                scm.appendNode('developerConnection', "scm:git://github.com/${project.organization}/${project.name}.git")

                def license = root.appendNode('licenses').appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')
            }
        }
    }
}

bintray {

    user = resolveProperty('bintrayUsername')
    key = resolveProperty('bintrayKey')
    publications = ['mavenJava']
    publish = true

    pkg {
        userOrg = "${project.bintrayUserOrg}"
        repo = "${project.repository}"
        name = "${project.name}"
        desc = "${project.description}"
        websiteUrl = "https://github.com/${project.organization}/${project.name}"
        issueTrackerUrl = "https://github.com/${project.organization}/${project.name}/issues"
        vcsUrl = "https://github.com/${project.organization}/${project.name}.git"
        licenses = ['Apache-2.0']
        labels = ['java', 'library']
        publicDownloadNumbers = true
        githubRepo = "${project.organization}/${project.name}"
        githubReleaseNotesFile = 'RELEASE_NOTES.md'

        version {
            released  = new Date()
            vcsTag = "v${project.version}"
            attributes = ["${project.name}": ["${project.group}:${project.name}:${project.version}"]]

            gpg {
                sign = true
                passphrase = resolveProperty('gpgPassphrase')
            }
        }
    }
}

String resolveProperty(String propKey) {
    String propValue = System.getenv()[propKey]
    if (propValue) { return propValue }
    hasProperty(propKey) ? getProperty(propKey) : null
}

